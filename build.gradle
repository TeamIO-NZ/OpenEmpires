import org.gradle.internal.os.OperatingSystem
plugins {
    id 'java'
}

project.ext.lwjglVersion = "3.2.3"
project.ext.jomlVersion = "1.9.19"
project.ext.steamworks4jServerVersion = "1.8.0"
project.ext.steamworks4jVersion = "1.8.0"
project.ext.commonsioVersion = "2.6"
project.ext.log4jVersion = "2.12.1"
project.ext.joptVersion = "6.0-alpha-3"
project.ext.fabricVersion = "0.6.3+build.168"
project.ext.gsonVersion       = "2.8.5"

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64")
                ? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
                : "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = System.getProperty("os.arch").contains("64") ? "natives-windows" : "natives-windows-x86"
        break
}


group 'tech.teamio'
version '1.0-SNAPSHOT'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.apache.logging.log4j:log4j-api:$log4jVersion"
    implementation "org.apache.logging.log4j:log4j-core:$log4jVersion"
    implementation "org.apache.logging.log4j:log4j-iostreams:$log4jVersion"
    annotationProcessor "org.apache.logging.log4j:log4j-core:$log4jVersion"

    implementation "commons-io:commons-io:$commonsioVersion"

    implementation "net.sf.jopt-simple:jopt-simple:$joptVersion"

    implementation "com.google.code.gson:gson:$gsonVersion"

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    implementation "org.joml:joml:${jomlVersion}"
    implementation "com.code-disaster.steamworks4j:steamworks4j-server:${steamworks4jServerVersion}"
    implementation "com.code-disaster.steamworks4j:steamworks4j:${steamworks4jVersion}"
}